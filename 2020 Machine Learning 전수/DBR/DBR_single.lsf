# Delete previous Bragg stack structures in the simulation file
switchtolayout;
groupscope("::model::stack_left");
deleteall;
groupscope("::model");

## Define the geometric parameters of the structure
mirror_periods = 15; #< Number of mirror periods in each Bragg stack
n0 = [2; 1]; #< Refractive indices per unit cell
dw = 1e-6; #< Length of unit cell
d0 = dw/4/n0; #< Quater wave optical length

## Specify mesh resolution 
target_dx = 1e-6/40;        	#< Target 40 cells per um in the refined regions
dx = d0(1) / round(d0(1)/target_dx); 	#< Make sure that actual dx is close to target but commensurate with the unit cell

## Construct arrays containing the thickness and refractive index of each layer
n_mirror = n0;
d_mirror = d0;
if(mirror_periods > 1) {
  for(i=1:mirror_periods-1) {
    n_mirror = [n_mirror; n0];
    d_mirror = [d_mirror; d0];
  }
}

## The parameters for a single mirror
n = [1;n_mirror;1];	#< Add vacuum (refractive index 1) to the left and right
d = [0;d_mirror;0];	#< The thickness of the first and last "layer" is always infinite, the actual value is ignored

# Set up Bragg stacks from scratch
x_span = sum(d);
for(i=2:length(d)-1) {
    if(n(i) > 1) {
        start_x = -x_span/2 + sum(d(1:i));
        addrect;
        set("x min",start_x);
        set("x max",start_x+d(i));
        set("index",2);
        set("y",0);
        set("y span",1e-6);
        set("name","structures");
        addtogroup("stack_left");
        }
}

select("mesh_left");
set("x min",-x_span/2+sum(d(1:2)));
set("x max",get("x min")+(mirror_periods)*sum(d0));
set("y",0);
set("y span",1e-6);
set("override x mesh",1);
set("override y mesh",0);
set("dx",dx);

select("monitor_T");
set("x", -x_span/2 + sum(d(1:2))+ d0(1)/2);

select("monitor_R");
set("x", -x_span/2 + sum(d(1:2)) - 1e-6);

select("source");
set("x", -x_span/2 + sum(d(1:2)) - 0.5e-6);
